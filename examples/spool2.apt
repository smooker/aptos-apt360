PARTNO TEST PROGRAM      $$ PROGRAM IDENTIFICATION
MACHIN/MMPOST,3              $$ POSTPROCESSOR AND OPTION TABLE
PRINT/ON 



$$RIGHT WINDOW DEFINITION******************

        $$PRINT DIMENSIONS
        FILLRD=.15
        A1=50
        R1=1.155
        D1=.602
        D2=.646
TPT=POINT/.301,.646 


YA = LINE / YAXIS
VRTL=LINE/PARLEL,YA,XLARGE,.301
ANGL=LINE/(POINT/0,D2),ATANGL,-(90-(A1/2))
C1=CIRCLE/0,0,R1
TOPFIL=CIRCLE/ XLARGE, VRTL, YLARGE, IN, C1, RADIUS, FILLRD
BOTFIL=CIRCLE/ XLARGE, ANGL, YSMALL, IN, C1, RADIUS, FILLRD
MIDFIL=CIRCLE/ XLARGE, VRTL, YLARGE, ANGL, RADIUS, FILLRD
SCIRC=CIRCLE/ CENTER, (POINT/.5,.646),TANTO,VRTL
PT1=POINT/SCIRC,ATANGL, 90
SPT=POINT/SCIRC,ATANGL,90,.1
EPT=POINT/SCIRC,ATANGL, -90
ENDL=LINE/SPT,EPT


$$END OF RIGHT WINDOW DEFINITION

$$START UP CODE
CLRP=PLANE/0,0,1,.1
CLRSRF/CLRP
TL=4   $$****TOOL NUMBER****
SEQNO/TL
SEQNO/ON
INSERTG0G49G80Z0G90G98
SEQNO/OFF
INSERT(7/32 2FL CARB EM)$$*****TOOL DESCRIPTION*****
LOADTL/TL
RAPID
GOTO/SPT
CLRSRF/NOW
SPINDL/ 4500, RPM,RANGE,2
OPSKIP/ON
COOLNT/FLOOD
OPSKIP/OFF
CUTTER/.218
PREFUN/43
CUTCOM/LENGTH,TL

TLONPS











$$ROTABL / ATANGL, 10.3


LSTRT=LINE/SPT,ATANGL,0
RAPID
FEDRAT/30.,IPM
PL1=PLANE/0,0,1,-.5
GOTO/(POINT/CENTER,SCIRC)
FROM/EPT
GO/TO,SCIRC,PL1
GOLFT/SCIRC,TANTO,VRTL
LP=1
 LOOP)
        GOFWD/VRTL,TANTO,MIDFIL
        GOFWD/MIDFIL,TANTO,ANGL
        GOFWD/ANGL,TANTO,BOTFIL
        GOFWD/BOTFIL,TANTO,C1
        GOFWD/C1,TANTO,TOPFIL
        GOFWD/TOPFIL,TANTO,VRTL
        LP=LP-1
        IF(LP)CONT,LOOP,LOOP
CONT)GOFWD/VRTL,TANTO,SCIRC
GOFWD/SCIRC,ON,ENDL
RAPID/
COOLNT/OFF
SPINDL/OFF
OPSTOP
DISPLY/ON
PPRINT TEST
 $$DRILL THE HOLES*************************

        $$PRINT DIMENSIONS
        
        C2=2.93  $$HOLE CIRCLE DIA
        
 DRILLC=CIRCLE/CENTER,(POINT/0,0),RADIUS,(C2/2)


LOADTL/2
SPINDL/4500,RPM,RANGE,2
COOLNT/FLOOD
CYCLE/DRILL,.4,20.,IPM,.1
DRILLP=PATERN/ARC,DRILLC,(90-20),(90+20),CLW,19
GOTO/DRILLP
CYCLE/OFF
COOLNT/OFF
REWIND
FINI

