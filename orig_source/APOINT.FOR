      SUBROUTINE APOINT(N)
C     *** THIS PROGRAM LAST MODIFIED FOR VERSION 4, MODIFICATION 3 ***
C
C
C...  THIS ROUTINE CONTAINS THE FIRST TEN POINT DEFINITIONS
C
      IMPLICIT REAL*8(A-H,O-Z)
       INTEGER PUNTAP
       DIMENSION DEFANS(82)
       COMMON/A1COM/REFSYS,NAME,NAME1,JSUBER,JSV,NAMSUB,KDBUG,INDXPT,
     1 LOOP,IFINI
       COMMON/ATAPTB/PROTAP,CANTAP,CLTAPE,POCTAP,PLOTAP,SRFTAP
     1, TAPES1,TAPES2,TAPES3,TAPES4,INTAPE,IOUTAP,PUNTAP
      COMMON/AVST/VST(7200)
      DIMENSION DEFTAB(1000)
       DIMENSION IDFTAB(2000)
      EQUIVALENCE(VST(7200),DEFTAB(1000))
       EQUIVALENCE (DEFTAB(1),IDFTAB(1))
      COMMON/ADFSTO/DEFSTO(85),PARTNO(11)
       DIMENSION IDFSTO(170)
      EQUIVALENCE(DEFSTO(4),DEFANS(1))
       EQUIVALENCE (DEFSTO(1),IDFSTO(1))
      COMMON/ADFPRG/DEFPRG(16)
      DIMENSION TVEC(3),SVEC(3),XYNOR(3),C3(3),
     1TNOR(3),PUVEC(3),V(3),V1(3),PUNOR(3),AMBDA(3),ANS(3),
     2SVECN(3),XO(3),XLAR(3),XSML(3),YLAR(3),YSML(3)
      EQUIVALENCE(DEFPRG(16),TVEC(16),SVEC(16),XYNOR(16),C3(16),
     1TNOR(13),PUVEC(13),V(13),V1(13),PUNOR(10),AMBDA(10),ANS(10),
     2SVECN(7),XO(7),XLAR(4),XSML(4),YLAR(4),YSML(4))
C***********************************************************************
C
       GO TO (100,200,320,405,520,600,700,802,915,1000,925,1200),N
C
C...  POINT/X,Y,Z
C
  100 DEFANS(1)= DEFTAB(2)
      DEFANS(2)= DEFTAB(3)
      DEFANS(3)= DEFTAB(4)
      GO TO 925
C
C...  POINT/X,Y
C
  200 DEFANS(1)= DEFTAB(2)
      DEFANS(2)= DEFTAB(3)
      CALL AZVALU
      GO TO 925
C
C...     POINT/INTOF,LN1,LN2
C
  320  DEFTAB(14) = 0.0
       DEFTAB(15) = 0.0
       DEFTAB(16) = 1.0
       DEFTAB(17) = 0.0
       ITYPE = 0
       GO TO 918
C
C...     POINT/X- OR YLARGE OR SMALL,INTOF,LN1,CIR1
C
C...  THIS ROUTINE ESTABLISHES A POINT BY THE INTERSECTION OF A LINE
C...  AND CIRCLE OR A PLANE AND A CYLINDER.
C
  405 CALL ADOT(DEFTAB(5),DEFTAB(5),CTHETA)
      IF(CTHETA) 410,995,410
  410 CALL ADOT(DEFTAB(10),DEFTAB(5),TEMP)
      DD=DEFTAB(8)-TEMP
       TEMP = DABS(DD) - DEFTAB(16)
            IF(DABS(TEMP)-.00001)  412, 412, 413
 412  T=0.0
       DD = DABS(DD)*DEFTAB(16)/DD
      GO TO 460
  413 IF(TEMP) 415,995,995
  415  T = DSQRT(DEFTAB(16)**2-DD**2)
      CALL ACROSS(DEFTAB(5),DEFTAB(13),TVEC)
      CALL ANORM(TVEC,TNOR)
            IF(IDFTAB(4)-31)  993, 420, 425
  420 IF(TNOR(1)) 455,995,460
  425       IF(IDFTAB(4)-32)  993, 430, 435
  430 IF(TNOR(1)) 460,995,455
  435       IF(IDFTAB(4)-35)  993, 440, 445
  440 IF(TNOR(2)) 455,995,460
  445       IF(IDFTAB(4)-36)  993, 450, 993
  450 IF(TNOR(2)) 460,995,455
  455 TNOR(1)=-TNOR(1)
      TNOR(2)=-TNOR(2)
      TNOR(3)=-TNOR(3)
  460 DEFANS(1)=DEFTAB(10)+(DD*DEFTAB(5))+(TNOR(1)*T)
      DEFANS(2)=DEFTAB(11)+(DD*DEFTAB(6))+(TNOR(2)*T)
      CALL AZVALU
      GO TO 925
C
C...     POINT/X- OR YLARGE OR SMALL,INTOF,CIR1,CIR2
C
  520 DO 525 J=1,2
      SVEC(J)=DEFTAB(J+12)-DEFTAB(J+4)
  525 CONTINUE
      SVEC(3)=0.0
      CALL ALNGTH(SVEC,SLEN)
      IF(SLEN-.00001) 995,5251,5251
 5251 SUMR=DEFTAB(11)+DEFTAB(19)
       DIFFR = DABS(DEFTAB(11)-DEFTAB(19))
            IF(DABS(SLEN-DIFFR)-.00001)  5255, 5255, 5254
 5254       IF(DABS(SLEN-SUMR)-.00001)  5252, 5252, 5253
C...     IF FIRST CIRCLE INPUT IS THE SMALLER, CHANGE SIGN OF
C...     DISTANCE BETWEEN CENTERS TO REVERSE VECTOR IN
C...     COMPUTATION OF POINT LOCATION
 5255       IF(DEFTAB(11).LT.DEFTAB(19)) SLEN = -SLEN
 5252 DEFANS(1)=DEFTAB(5)+DEFTAB(11)*(DEFTAB(13)-DEFTAB(5))/SLEN
      DEFANS(2)=DEFTAB(6)+DEFTAB(11)*(DEFTAB(14)-DEFTAB(6))/SLEN
      GO TO 585
 5253 IF(SLEN-SUMR) 526,995,995
  526 IF(SLEN**2-(DEFTAB(11)-DEFTAB(19))**2) 995,527,527
  527 CALL ANORM(SVEC,SVECN)
      R1SQR=DEFTAB(11)**2
      R2SQR=DEFTAB(19)**2
      SLENSQ=SLEN**2
      D=(R1SQR-R2SQR+SLENSQ)/(2.0*SLEN)
      CALL ACROSS(DEFTAB(8),SVECN,PUVEC)
      CALL ANORM(PUVEC,PUNOR)
            IF(IDFTAB(4)-31)  993, 530, 535
  530 IF(PUNOR(1)) 565,995,570
  535       IF(IDFTAB(4)-32)  993, 540, 545
  540 IF(PUNOR(1)) 570,995,565
  545       IF(IDFTAB(4)-35)  993, 550, 555
  550 IF(PUNOR(2)) 565,995,570
  555       IF(IDFTAB(4)-36)  993, 560, 993
  560 IF(PUNOR(2)) 570,995,565
  565 PUNOR(1)=-PUNOR(1)
      PUNOR(2)=-PUNOR(2)
      PUNOR(3)=-PUNOR(3)
  570 DO 575 J=1,3
       DEFANS(J) = DEFTAB(J+4) + (SVECN(J)*D) + (PUNOR(J)*DSQRT(R1SQR
     1 -D**2))
  575 CONTINUE
  585 CALL AZVALU
      GO TO 925
C
C...     POINT/CIR1,ATANGL,A
C
  600  CONST = DEFTAB(11)/57.29577951289617
       DEFANS(1) = DEFTAB(3) + (DEFTAB(9)*DCOS(CONST))
       DEFANS(2) = DEFTAB(4) + (DEFTAB(9)*DSIN(CONST))
      CALL AZVALU
      GO TO 925
C
C...     POINT/CENTER,CIR1
C
  700 DEFANS(1)= DEFTAB(4)
      DEFANS(2)= DEFTAB(5)
      CALL AZVALU
      GO TO 925
C
C...     POINT/RTHETA,XY-, YZ-, OR ZXPLAN,R,A
C
 1000       IF(IDFTAB(4).EQ.106) GO TO 1010
            IF(IDFTAB(4).NE.107) GO TO 993
      SAVE = DEFTAB(4)
      DEFTAB (4) = DEFTAB(5)
      DEFTAB(5) = SAVE
 1010  A = DEFTAB(4)*DCOS(DEFTAB(5)/57.29577951289617)
       B = DEFTAB(4)*DSIN(DEFTAB(5)/57.29577951289617)
            IF(IDFTAB(6)-33)  993, 110, 101
  101       IF(IDFTAB(6)-37)  993, 120, 102
  102       IF(IDFTAB(6)-41)  993, 130, 993
  110 DEFANS(1)=A
      DEFANS(2)=B
C... DEFANS(3)=0
      GO TO 925
  120 DEFANS(2)=A
      DEFANS(3)=B
C... DEFANS(1)=0
      GO TO 925
  130 DEFANS(3)=A
      DEFANS(1)=B
C... DEFANS(2)=0
      GO TO 925
C
C...     POINT/X- OR YLARGE OR SMALL,INTOF,LN1,ANY OF THE
C...           SURFACES WITH THE QUADRIC CANONICAL FORM
C
  802       IF(IDFTAB(4)-31)  993, 805, 810
  805 ASSIGN 855 TO K
      GO TO 838
  810       IF(IDFTAB(4)-32)  993, 815, 820
  815 ASSIGN 860 TO K
      GO TO 838
  820       IF(IDFTAB(4)-35)  993, 825, 830
  825 ASSIGN 865 TO K
      GO TO 838
  830       IF(IDFTAB(4)-36)  993, 835, 993
  835 ASSIGN 870 TO K
  838 XYNOR(1)=0.0
      XYNOR(2)=0.0
      XYNOR(3)=1.0
C
C...  CALCULATE  AMBDA,MU,NU
C
      CALL ACROSS(DEFTAB(5),XYNOR,V)
      CALL ANORM(V,AMBDA)
      DO 839 J=1,3
      XO(J)=DEFTAB(J+4)*DEFTAB(8)
  839 CONTINUE
C
C...  CALCULATE T  FOR A,B,C TERMS IN QUARDRATIC FORMULA
C
      A=(DEFTAB(10)*(AMBDA(1)**2)+(DEFTAB(11)*AMBDA(2)**2)
     1  +(2.0 *DEFTAB(16)*AMBDA(1)*AMBDA(2)))
      B=(2.0*DEFTAB(10)*XO(1)*AMBDA(1))+(2.0*DEFTAB(11)*XO(2)
     1  *AMBDA(2))+(2.0*DEFTAB(16)*AMBDA(1)*XO(2))+(2.0*
     2  DEFTAB(17)*AMBDA(1))+(2.0*DEFTAB(18)*AMBDA(2))+(2.0*
     3  DEFTAB(16)*XO(1)*AMBDA(2))
      C=(DEFTAB(10)*XO(1)**2)+(DEFTAB(11)*XO(2)**2)+(2.0*
     1  DEFTAB(16)*XO(1)*XO(2))+(2.0*DEFTAB(17)*XO(1))
     2  +(2.0*DEFTAB(18)*XO(2)+DEFTAB(13))
      IF(B**2-(4.0*A*C)) 995,840,840
  840  RAD = DSQRT(B**2-(4.0*A*C))
      IF(A) 841,842,841
  842 TP=-C/B
      CALL DVCHK (K000FX)
      GO TO (995,8420) ,K000FX
 8420 TM=TP
      GO TO 843
  841 TP=(-B+RAD)/(2.0*A)
      TM=(-B-RAD)/(2.0*A)
  843 DO 845 I=1,3
      DEFANS(I)=XO(I)+(AMBDA(I)*TP)
  845 CONTINUE
      DO 850 I=4,6
      DEFANS(I)=XO(I-3)+(AMBDA(I-3)*TM)
  850 CONTINUE
      DO 852 J=1,3
      ANS(J)=DEFANS(J)-DEFANS(J+3)
  852 CONTINUE
C...  ZERO OUT VECTOR ARRAYS
      DO 853 I = 1,3
      XLAR(I) = 0.0
      XSML(I) = 0.0
      YLAR(I) = 0.0
  853 YSML(I) = 0.0
      GO TO K,(855,860,865,870)
  855 XLAR(1)=1.0
C...  XLAR(2)=0.0
C...  XLAR(3)=0.0
      CALL ADOT(ANS,XLAR,TEMP)
      GO TO 872
  860 XSML(1)=-1.0
C...  XSML(2)=0.0
C...  XSML(3)=0.0
      CALL ADOT(ANS,XSML,TEMP)
      GO TO 872
C...  YLAR(1)=0.0
  865 YLAR(2)=1.0
C...  YLAR(3)=0.0
      CALL ADOT(ANS,YLAR,TEMP)
      GO TO 872
C...  YSML(1)=0.0
  870 YSML(2)=-1.0
C...  YSML(3)=0.0
      CALL ADOT(ANS,YSML,TEMP)
  872 IF(TEMP)875,885,885
  875 DO 880 I=1,3
      DEFANS(I)=DEFANS(I+3)
  880 CONTINUE
  885 CALL AZVALU
      GO TO 925
C
C...     POINT/INTOF,PLN1,PLN2,PLN3
C
  915  ITYPE = 1
  918  DO 920  I = 1,4
       DEFTAB(I+99) = DEFTAB(I+3)
       DEFTAB(I+103) = DEFTAB(I+8)
  920  DEFTAB(I+107) = DEFTAB(I+13)
       CALL ASIMEQ
            IF(JSUBER.NE.0) GO TO 925
       DEFANS(1) = DEFTAB(112)
       DEFANS(2) = DEFTAB(113)
       DEFANS(3) = DEFTAB(114)
            IF(ITYPE.NE.0) GO TO 925
       CALL AZVALU
      GO TO 925
C
  993  JSUBER = 1003
      GO TO 925
  995  JSUBER = 1005
       GO TO 925
C
C...     POINT/PATTERN,N
C
C...     PICK UP POINT NUMBER GIVEN AFTER PATTERN, CONVERT TO INTEGER
 1200  JTEMP = DEFTAB(6)
C...     COMPUTE PICK-UP INDEX FROM POINT NUMBER
       JTEMP = 3*JTEMP - 2
C...     LOCATE PATTERN DEFINITION ON LARGE SURFACE FILE
       CALL ASERCH(SRFTAP,IDFTAB(6),IOFLAG)
C...     TEST FOR ERROR IN FILE SEARCH OPERATION
            IF(IOFLAG.LT.0) GO TO 1210
       JSUBER = 840
       GO TO 925
C...     DEFINITION FOUND - READ IT IN
 1210  CALL ATAPRD(SRFTAP,IOFLAG,JTEMP1,3,IDFTAB(1),3,DEFSTO(10),2
     1, DEFTAB(1),0)
C...     TEST FOR ERROR WHEN READING IN PATTERN
           IF(IOFLAG.LT.0) GO TO 1215
       JSUBER = 841
       GO TO 925
C...    IS POINT NUMBER WITHIN RANGE OF PATTERN
 1215 IF(JTEMP.LE.JTEMP1) GO TO 1220
      JSUBER = 154
      GO TO 925
C...     PICK UP DESIRED POINT FROM PATTERN
 1220  DEFANS(1) = DEFTAB(JTEMP)
       DEFANS(2) = DEFTAB(JTEMP+1)
       DEFANS(3) = DEFTAB(JTEMP+2)
C...     TEST FOR PATTERN DEFINED IN ANOTHER REFERENCE SYSTEM
            IF(DEFSTO(2).EQ.REFSYS) GO TO 925
C...     YES, IT WAS - TRANSFORM POINT
       IDFSTO(1) = 1
       CALL ATRFRM
C
  925  RETURN
       END

