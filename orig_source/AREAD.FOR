      SUBROUTINE AREAD
C     *** THIS PROGRAM LAST MODIFIED FOR VERSION 4, MODIFICATION 0 ***
C
C...  THIS PROGRAM PROCESSES READ/1 OR READ/1,LIST STATEMENTS AND
C...  GENERATES CLASS 14 OR CLASS 12 AND 13 PTPP ENTRIES AS THE
C...  INPUT IS A SURFACE OR SCALAR DEFINITION, RESPECTIVELY.
C
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 INWORD,NEWONE,NAME,IDIS,MACNAM,KRFSYS
      INTEGER AMATOD,AEXTRA,AMDTOA,AEXTRD,AEXCH,DEBUG,SUM
      INTEGER PUNTAP
C
      COMMON/ATAPTB/PROTAP,CANTAP,CLTAPE,POCTAP,PLOTAP,SRFTAP,
     1TAPES1,TAPES2,TAPES3,TAPES4,INTAPE,IOUTAP,PUNTAP
      COMMON/A1COM/NAME,KRFSYS,SUM,JSUBER,NAMSUB,DEBUG,INDXPT,LOOP,IFINI
      COMMON/AMXTAB/MAXTAB,MAXVS1,MAXVS2,MAXPTP,MAXSCL,MAXVS,MAXVST,MXPT
     1PP,MXCAN,MXSRCN,MAXSLR,MAXELM,MAXPOT
      COMMON/AVST/VST(2750),PTPP(2225),CANON(2225)
      COMMON/AILMTB/ELMENT(600),ICLASS(600),JLMENT
      COMMON/APTPP/PT(300),INPTP,NW,JMODE,KLASS,KLASTP,IRECN,NPT,NPR
      COMMON/ABREAK/NEXTNW,NRELCN,KANLCN,LOCSEQ,NEXTCN,NWONRD,LPLOCN,
     1 JSW4,ISN,K,MXLOOP,JSW2,JSAV,JSW1,MAXMV,MXNEST,INILL,KF
      COMMON/ALIBRY/AMACVR(50),AMACVL(50),MACICL(50),DEFNAM,SEGNAM
     1 ,IVARCT,ISYSMC,IRDMOD,LUUP,NWUNRD,NOLIBR
C
       DIMENSION IPT(600)
      DIMENSION CARD(10)
       EQUIVALENCE (PT(1),IPT(1))
C
      INTEGER*4 TYPE1/Z00000001/
      INTEGER*4 SLASH/Z00000304/
      DATA ENDPCH/'(ENDPCH)'/
      INTEGER*4 COMMA/Z00000904/
C
C...  IF KSUBER IS NOT ZERO THEN ALL PUNCH BLOCK CARDS WILL FLUSH THRU
C...  IN SEARCH OF THE ENDPCH CARD WHICH WILL CAUSE A RETURN TO CALLING
C..    PROGRAM
      KSUBER = 0
      NAME = 0.0
      SUBS = 0.0
      NPT = 0
      INILL=INILL+2
C...  TEST FOR THE REQUIRED SLASH FOLLOWING MAJ. WORD..READ.
      IF (ICLASS(INILL-1).NE.SLASH) GOTO 2155
C     MUST HAVE READ/1 OR READ/2
      IF (IATYPE(INILL).NE.3) GO TO 3000
      IF (ELMENT(INILL).EQ.1.0D0) GO TO 5
      IF (ELMENT(INILL).NE.2.0D0) GO TO 3000
C...  BRANCH IF READ/2 ALREADY IN EFFECT
      IF (IRDMOD.GT.0) GO TO 3030
C...  BRANCH IF A READ/2 STATEMENT WAS FOUND IN A LOOP OR MACRO
      IF (LOOP.GT.0) GO TO 3037
C     CHECK FOR COMMA AFTER READ/2
      IF (ICLASS(INILL+1).NE.COMMA) GO TO 3000
C     READ/2,MEMNAME - CHECK FOR MEMBER NAME
      IF (IATYPE(INILL+2).NE.1) GO TO 3000
      IF ((INILL+2).NE.JLMENT) GO TO 3040
C     SAVE MEMBER NAME
      SEGNAM = ELMENT(INILL+2)
C     SET FLAG TO READ FROM PDS
      IRDMOD = 1
C     RETURN - NEXT INPUT WILL COME FROM SEGMENT PDS
      RETURN
    5 INILL = INILL +1
 10   NAME=0.
      SUBS = 0.
      J=0
C..   BRANCH TO GET NAME VAR. FROM HEX CARD, (THAT IS, THERE IS NO
C...  NAME VARIABLE TO BE GOTTEN FROM THE READ/1 STATEMENT)
      KP=0
      IF(INILL.GT.JLMENT) GOTO 50
C...   CHECK FOR SEPARATING COMMA--READ/1,VAR1,VAR2,ETC......
      IF(ICLASS(INILL).NE.COMMA) GOTO 2000
      INILL=INILL+1
      IF(INILL.GT.JLMENT) GOTO 2000
C... CHECK FOR REQUIRED VAR.SYMBOL FOLLOWING THE COMMA
      IF (ICLASS(INILL).NE.TYPE1) GOTO 2000
C     PICK UP SYMBOL IN LIST
      NAME=ELMENT(INILL)
      INILL=INILL+1
C     SUBSCRIPT
      IA=IATYPE(INILL)-6
      IF(IA.LT.1) GOTO 50
      INILL=INILL+1
      GOTO(15,20,30),IA
C...   SCALAR SUBSCRIPT SET PTPP J INDICATOR TO 2
 15   J=2
      GOTO 40
C... SUBSCRIPT IS NUMERIC,,SET SUBS TO THE VALUE
   20 SUBS = ELMENT(INILL-1)
      GOTO 50
C...  SUBSCRIPT IS AN EXPRESSION SET PTPP INDICATOR TO 3
 30   J=3
C.. SET WORKING STORAGE INDEX  THAT IS,  FLAG KP
C     THIS IS DONE FOR A SCALAR OR EXPRESSION SUBSCRIPT ONLY
40       KP = AEXTRA(ELMENT(INILL-1),4)
C...
C     PICK UP DEFINITION
C..  BELOW IS CARD ARRAY LAYOUT
C        CARD(1) = VAR. NAME  IN EBDIC ,,
C.       CARD(2) = FL. PT. SUBSCRIPT  - HEX REPRESENTATION
C.       CARD(3) = SURF TYPE, NO  - HEX
C.       CARD(4) = REFSYS FLAG - GENERALLY IS HEX ZEROES
C.       CARD(5) = 1ST FL.PT. PARAM  - HEX REP.
C.       CARD(6) = 2ND    ''
C.       CARD(7) = 3RD    ''      IF APPLICABLE
C.       CARD(8) = 4TH     ''      ''
C.       CARD(9) = 5TH    ''       ''
C.. THE SECOND CARD AS FOLLOWS
C.       CARD(1) = 6TH FL. PT. PARAM. IN HEX ,,IF APPLICABLE
C.       CARD(2) = 7TH   ''        '''       ''
C.       CARD(3) = 8TH   ''        '''       ''
C..   AND SO ON AND SO FORTH UNTIL DEFINITION IS COMPLETED,
C..  SE PART PROGRAMMERS GUIDE FOR LIST OF CANONICAL FORMS
C..
   50 IF (IRDMOD.NE.1) GO TO 53
      M = 1
   51 IRDMOD = 2
      CALL APREAD (CARD(1),IRDMOD,SEGNAM,JSUBER)
      IF (JSUBER.NE.0) GO TO 3035
      IF (IRDMOD.EQ.0) GO TO 52
      IRDMOD =1
      GO TO (54,196,2026),M
   52 GO TO (53,195,2025),M
   53 READ (INTAPE,60) (CARD(I),I=1,9)
   60 FORMAT (9A8)
C     IS THIS END OF DATA
   54 IF (CARD(1).EQ.ENDPCH) GO TO 1000
C...   THIS IS NOT THE ENDPCH CARD, PROCESS HEX CARD IF NO ERROR EXISTS
C...  FLUSH HEX CARDS IF ERROR CONDITION PREVAILS ON READ/1 STATEMENT
      IF (KSUBER.NE.0) GO TO 50
C     IF NAME=0., NO SYMBOL IN LIST - USE SYMBOL IN DEF.
   61 IF ( NAME.NE.0.) GO TO 70
   62 NAME = CARD(1)
      SUBS = CARD(2)
   70 CALL AVS1PT(JK)
      NO = AEXTRA(CARD(3),4)
C..  IF SUBSCRIPT IS NUMERIC THEN SET PTPP INDICATOR TO 1,(ALWAYS
C           A NUMERIC SUBSCRIPT  VALUE WHEN GIVEN ON HEX CARD)
      IF ( SUBS.NE.0.) J = 1
      IF (J.NE.0) GO TO 90
C... TEST NON-SUBSCRIPTED VARIABLE
      GOTO ( 100,2015,2107,100,2251,2109,2102,2105,2105,2105 ) ,JK
C... TEST SUBSCRIPTED VARIABLES
   90 GOTO (2108,2015,2108,2108,2251,2108,2102,2105,2106,2106) ,JK
C...
C...
C.. INITIALIZE VST ENTRY FOR A DIMENSIONED SCLAR (DONE ONCE PER VAR.)
   99 CALL APRFIX(1,IRET)
      MAXSCL = MAXSCL + IABS(AEXTRA(VST(NAMSUB+1),4))
      GO TO 110
C...
C...DEFINE NAME...BRANCH IF FOR A SURFACE,OTHERWISE IT IS SCALAR ENTRY
  100 IF (NO.NE.4) GOTO 140
C..
C..
C...  UPDATE NON-SUBSCRIPTED SCALAR TABLE
  101 MAXSCL = MAXSCL + 1
      VST(NAMSUB+1) = AFULL8(2,MAXSCL)
C...
 110  INPTP=5
      JMODE= 5
C...     SET UP CLASS 13 PTPP RECORD FOR SCALAR
       PT(2) = 0
       PT(3) = CARD(5)
       IPT(7) = 2
       IPT(8) = J
       IPT(9) = KP
       IPT(10) = NAMSUB
      IF(J.NE.1) GOTO 120
       PT(6) = SUBS
      INPTP=6
  120  IPT(1) = 13
       IPT(2) = INPTP - 1
      CALL APTPUT
C...  READ NEXT DEFINITION
      GOTO 10
C...
C..
C... END OF SCALAR PROCESSING
C..****         ******         *****         *****         ****
C..
C..          P R O C E S S     S U R F A C E
C..
C... INITITIALIZE VST ENTRY FOR A DIMENSIONED SURFACE
  130 CALL APRFIX (2,IRET)
      GOTO 150
C..
C... NON-DIMENSIONED SURFACE..UPDATE VST INDEX
  140 VST(NAMSUB+1) = AFULL8(7,0)
C..
C.. OUTPUT PTPP CLASS 14 RECORD
C..
C..  NO  IS COUNT OF ENTRIES ASSOCIATED WITH THIS DEFINITION..
C..  FOR SCALARS AND NON-LARGE SURFACES THE VALUE REPRESENTS..3 HEADER
C... ENTRIES ( ALWAYS ) PLUS THE NUMBER OF FL.PT. PARAMS IN THE DEF.
 150  IF(NO.NE.5) GOTO 160
C..  THIS IS A LARGE SURFACE DEFINITION
C..    NO - IS EXACT NO.OF PARMS TO PROCESS,ADD 3 FOR HEDER INFO
         NO = AEXTRA(CARD(4),4)+3
C..
 160  NO=NO-3
C.. COMPUTE NW,,THE TOT. NO. OF 8-BYTE WD.S IN THIS PTPP CLASS 14 REC.
C..   MUST BE 2 PTPP ENTRIES TO DESCRIBE EACH INPUT PARAMETER
C..NO IS THE NO. OF INPUT PARAMETERS,,4 IS NO. OF ENTRIES FOR PTPP HDR,
      NW = 2*NO + 4
C.. PTPP OUTPUT 1ST PORTION OF DATA TO PTPP FOR THIS SURF DEFINITION
      JMODE = 1
      PT(1) = AFULL8(14,0)
      PT(2)=AFULL8(0,NO)
      PT(3)=AFULL8(4,J)
      PT(4)=AFULL8(KP,NAMSUB)
      PT(5)=CARD(3)
      INPTP = 6
      IF(J.NE.1) GOTO 170
      PT(5)=SUBS
      PT(6)=CARD(3)
      INPTP = 7
      NW = NW + 1
  170 NO = NO + 4
      IS=5
      IE=9
      IF(IE.GT.NO) IE=NO
      IN=IE
 180  DO 190 J=IS,IE
      PT(INPTP) = AFULL8(1,0)
      PT(INPTP+1) = CARD(J)
  190 INPTP = INPTP + 2
      INPTP = INPTP - 1
      IF(IN.EQ.NO) GOTO 200
      CALL APTPUT
      JMODE = 2
      INPTP = 1
      IS=1
      IN=IN+9
      IF (IRDMOD.NE.1) GO TO 195
      M = 2
      GO TO 51
  195 READ (INTAPE,60) (CARD(I),I=1,9)
C     IS THIS END OF DATA
  196 IF (CARD(1).EQ.ENDPCH) GO TO 1000
      IF(IN.LT.NO) GOTO 180
C     LAST CARD FOR THIS DEFINITION
         IE = NO+9-IN
      IN=NO
      GOTO 180
C... THIS IS THE FINAL DATA TO BE MOVED TO PTPP FOR THIS SURF. DEF.
  200 JMODE = 3
      CALL APTPUT
      GOTO 10
C..
C.. IF READ ZARG. LIST IS NOT COMPLETED, PRINT 1ST DANGLING NAME AND
C..   RETURN TO THE CALLING PROGRAM.
 1000 IF (NAME.EQ.0.) RETURN
      JSUBER = 2002
      GOTO 2017
C...
C.. I/O FORMAT ERROR, FLUSH REMAINING DATA CARDS
C..
 2000 JSUBER = 2000
      GOTO 2016
C....
C
C.. VST OVFLO, OUTPUT OFFENDING VARIABLE AND FLUSH REMAINING DATA CARDS
C..
 2015 JSUBER = 15
 2016 KSUBER = 1
C.. SUM = 9 INDICATES TO ADIAGP TO OUPUT  NAME AND SUBSCRIPT
 2017 SUM = 9
C..  PT(6) IS THE NAME AND IS SET BY ADIAGP, SUBS VAL. MUST BE SET HERE
      PT(7) = SUBS
C.. SUM = 1 INDICATES  VARIABLE NAME WAS NOT SUBSCRIPTED
      IF (SUBS.EQ.0.) SUM = 1
C.. SUM = 0 INDICATES NO VARIABLE ASSOCIATED WITH THIS ERROR MESSAGE
      IF(NAME.EQ.0.) SUM=0
C..  OUTPUT CLASS2 PTPP RECORD....(ERROR DIAGNOSTIC)
      CALL ADIAGP
C.. RESET VARIABLES TO PREVENT REDUNDANT ERROR MESSAGES FROM FUTHER
C     PROCESSING BY OTHER SUBROUTINES..
      JSUBER = 0
      NAME = 0.0
C.. ALL ERROR MESSAGES HAVE BEEN PROCESSED, ARE THERE MORE DATA CARDS
      IF (CARD(1).EQ.ENDPCH) RETURN
C..  THERE ARE MORE DATA CDS...PROCESS OR FLUSH THEM ALL
      IF (KSUBER.NE.0) GOTO 50
C.. PROCESS THEM..BUT DELETE THE ONES ASSOCIATED WITH THIS DEFINITION
      IF ((NO.LE.8).AND.(NO.NE.5)) GO TO 10
C... ERROR VAR. WAS A SURF. DEF. OTHER THAN A POINT,VECTOR,LINE,
C...  PLANE, OR SPHERE...DELETE THE ASSOCIATED PARAMETER CARDS.
C...    THEN READ-IN NEXT HEX CARD AND PROCESS IT IN THE NORMAL MODE.
C..
C..
C...ERROR  READ-LOOP  AREA
C..
C... CALC. NO. OF PARAMETERS FOR THIS SURF. DEF.
C... FOR A LARGE SURF. CARD(4) IS THE EXACT NO. OF PARMS. TO BE GOTTEN
C..  ADD 3 TO ALLOW FOR HEADER ENTRIES ASSOCIATED WITH THIS LRG.SURF.
      IF (NO.EQ.5) NO = AEXTRA(CARD(4),4)+3
      IN = 9
C... ADD 1 TO PARM COUNT, THIS ALLOWS FOR FIELD ON CARD SET ASIDE FOR
C..  REFSYS FLAG WHICH HAS NOT BEEN INCLUDED IN CAN.FORM LENGTH COUNT
      NO = NO + 1
C... DESTRUCTIVE READ-IN  OF CARD ARRAY
 2020 IF (IRDMOD.NE.1) GO TO 2025
      M = 3
      GO TO 51
 2025 READ (INTAPE,60) (CARD(I),I=1,9)
C..   ENDPCH STATEMENT TERMINATES READING OF HEX CARDS
 2026 IF (CARD(1).EQ.ENDPCH) GO TO 1000
      NO = NO - IN
C..   HAVE WE READ-IN THE LAST CARD ASSOCIATED WITH THIS DEFINITION
      IF (NO.LE.IN) GOTO 10
C..  READ-IN NEXT CARD ASSOCIATED WITH THIS DEFINITION
      GO TO 2020
C...
C....******           *****           ****       ****          ***
C          ERROR   MESSAGES
C..
C...
C.. O.K. IF VAR. IS DIMEN. BUT NOT USED,, ER.211 IF VAR IS MACRO NAME
C..  TEST VST ENTRY TO SEE IF THIS IS A DIMENSIONED VAR.,BR. TO 2103 IF
C.. IT IS, OTHERWISE IT IS A MACRO NAME AND IS IN ERROR.
 2102 IF (AEXTRA(VST(NAMSUB+1),4).LT.0) GO TO 2103
      JSUBER = 211
      GO TO 2017
C... DIMENSIONED ARRAY NOT USED,INITIALIZE VST ENTRY TO DIM.SURF.
 2103 IF (J.EQ.0) GO TO 2105
C... SUBSCRIPTED, SCALAR OR SURF.( BR. IF .SURF.)
      IF (NO.NE.4) GOTO 130
C.. INITIALIZE VST ENTRY FOR A DIMEN.SCALAR
      GOTO 99
C...
C..
C... NO SUBSCRIPT VALUE GIVEN WITH INPUT VAR. AND IT NEEDS ONE
 2105 JSUBER = 105
      GO TO 2017
C...
C... VST SHOWS A PREVIOUSLY DEFINED SCALAR OR SURF. (DIMENSIONED)
C.. BR TO NORM PROCESS THIS DIMENSIONED SCALAR IF VST ENTRY
C...    SHOWS ARRAY IS  FOR SCALARS
 2106 IF ((NO.EQ.4).AND.(JK.EQ.9)) GOTO 110
C... BR TO 150 TO NORMAL PROCESS THIS HEX DIMENSIONED SURF. VAR., IF
C...  VST ENTRY SHOWS ARRAY IS FOR SURFACES
      IF ((NO.NE.4).AND.(JK.EQ.10)) GOTO 150
C..
C.. SCALAR ARRAY CONTAINS A SURFACE OR VISA VERSA
C..
      JSUBER = 106
      GO TO 2017
C...
C..
C.. NON-SUBSC. VAR. WAS A DEFINED SURF..O.K. IF CURRENT DEF. IS SAME
 2107 IF (NO.EQ.4) GO TO 2110
C.. THIS MUST BE A SURFACE DEF., OUTPUT A CLASS 14 PTPP REC.
      GO TO 150
C...
C...
C     SUBSCRIPT FOUND FOR VAR. NAME WHICH WAS NOT GIVEN IN A RESERV STMT
 2108 JSUBER = 108
      GOTO 2017
C...
C...
C..NON-SUBSCR. VAR. WAS DEFINED A SCALAR,O.K. IF CURRENT DEF. IS SAME
 2109 IF (NO.EQ.4) GO TO 101
C... ERROR..TRIED TO REDEFINE A SCALAR AS A SURFACE OR VISA VERSA
 2110 JSUBER = 2001
      GO TO 2017
C...
C...
C        I/O FORMAT ERROR NO SLASH FOLLOWING MAJ.VOC.WORD READ
C...   FLUSH REMAINING HEX DATA CARDS
 2155 JSUBER = 155
         IF (ICLASS(INILL).EQ.1.0D0) GO TO 2016
      GO TO 3035
C...
C.. VAR. NAME IS A PREV. DEFINED STMT ID
 2251 JSUBER = 251
      GOTO 2017
 3000 CARD(1) = ENDPCH
      GO TO 2000
C...  READ/2 CANNOT READ IN ANOTHER READ/2 STATEMENT
 3030 JSUBER = 30
 3035 CARD(1) = ENDPCH
      GO TO 2016
C...  A READ/2 STATEMENT WAS FOUND IN A LOOP OR MACRO
 3037 JSUBER = 37
      GO TO 3035
C...  TOO MANY ENTRIES ON READ/2 STATEMENT
 3040 JSUBER = 34
      GO TO 3035
      END

