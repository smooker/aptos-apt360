      SUBROUTINE AMATX(N)
C     *** THIS PROGRAM LAST MODIFIED FOR VERSION 4, MODIFICATION 3 ***
C
      IMPLICIT REAL*8(A-H,O-Z)
       COMMON /ATMATY/XMAT4(16),XMAT3(16),XMAT2(16),XMAT1(16),TMATX(16)
       DIMENSION DEFANS(82)
       COMMON/A1COM/REFSYS,NAME,NAME1,JSUBER,JSV,NAMSUB,KDBUG,INDXPT,
     1 LOOP,IFINI
      COMMON/AVST/VST(7200)
      DIMENSION DEFTAB(1000)
       DIMENSION IDFTAB(2000)
      EQUIVALENCE(VST(7200),DEFTAB(1000))
       EQUIVALENCE (DEFTAB(1),IDFTAB(1))
      COMMON/ADFSTO/DEFSTO(85),PARTNO(11)
      EQUIVALENCE(DEFSTO(4),DEFANS(1))
C***********************************************************************
C...     SET STORING INDEX TO START OF DEFANS, AND PICKUP
C...     INDEX TO START OF DEFTAB
       MS = 0
       LM = 1
C...     SET FLAG TO INDICATE NO MATRIX PRODUCT
       NOMAT = 0
       INTRA = 0
       INROT = 0
            IF(N.GE.7) GO TO 10
       GO TO (100,200,300,400,500,600),N
   10       IF(N.LT.31) GO TO 800
       N = N - 30
       GO TO (730,900,925,950),N
C
C...     SET FLAG TO MULTIPLY MATRICES AFTER NEXT ONE DEFINED
   50  NOMAT = 1
C...     SET STORING INDEX FOR SECOND MATRIX
       MS = 12
C...     SET PICKUP INDEX FOR SECOND MATRIX
       LM = NXLOC
C...     BRANCH TO DEVELOP SECOND MATRIX
       GO TO (870,300,200,205,400),NEXT
C
C...     MATRIX/A1,B1,C1,D1,A2,B2,C2,D2,A3,B3,C3,D3
C
  100 DO 110 K = 1,12
      DEFANS(K)=DEFTAB(K+1)
  110 CONTINUE
       GO TO 1000
C
C...     MATRIX/TRANSL,A,B, OR TRANSL,A,B,C
C
  200  DEFANS(MS+12) = DEFTAB(LM+4)
       GO TO 210
  205  DEFANS(MS+12) = 0.0
  210  DEFANS(MS+1) = 1.0
C...   DEFANS(MS+2) = 0
C...   DEFANS(MS+3) = 0
       DEFANS(MS+4) = DEFTAB(LM+2)
C...   DEFANS(MS+5) = 0
       DEFANS(MS+6) = 1.0
C...   DEFANS(MS+7) = 0
       DEFANS(MS+8) = DEFTAB(LM+3)
C...   DEFANS(MS+9) = 0
C...   DEFANS(MS+10) = 0
       DEFANS(MS+11) = 1.0
       INTRA = 1
C...     TEST FLAG TO SEE WHETHER TO GO TO TAKE MATRIX PRODUCT,
C...     OR TO EXIT
            IF(NOMAT)  50, 1000, 510
C
C...     MATRIX/XY-, YZ-, OR ZXROT,A
C
  300  CON1 = DEFTAB(LM+2)/57.29577951289617
       CON2 = DCOS(CON1)
       CON3 = -DSIN(CON1)
       INROT = 1
C...     TEST FOR AN ANGLE OF ZERO OR AN INTEGRAL MULTIPLE OF 180
       TEMP = DMOD(DEFTAB(LM+2),180.0D0)
            IF(TEMP.NE.0.0) GO TO 310
C...     YES - SET SINE AND COSINE TO EXACT VALUES
       CON3 = 0.0
       CON2 = DSIGN(1.0D0,CON2)
       GO TO 320
C...     ANGLE IS NOT MULTIPLE OF 180 - SEE IF 90 IS A FACTOR
  310  TEMP = DMOD(DEFTAB(LM+2),90.0D0)
            IF(TEMP.NE.0.0) GO TO 320
C...     IT IS - SET SINE AND COSINE TO EXACT VALUES
       CON2 = 0.0
       CON3 = DSIGN(1.0D0,CON3)
  320  LDF4 = 2*LM - 1
            IF(IDFTAB(LDF4+3).EQ.38) GO TO 360
            IF(IDFTAB(LDF4+3).EQ.34) GO TO 365
            IF(IDFTAB(LDF4+3)-42)  393, 370, 393
C
C...  CALCULATE YZ ROT
C
  360  DEFANS(MS+1) = 1.0
       DEFANS(MS+6) = CON2
       DEFANS(MS+7) = CON3
       DEFANS(MS+10) = -CON3
       DEFANS(MS+11) = CON2
C...   DEFANS(MS+2,3,4,5,8,9,12) = 0
       GO TO 380
C
C...  CALCULATE XYROT
C
  365  DEFANS(MS+1) = CON2
       DEFANS(MS+2) = CON3
       DEFANS(MS+5) = -CON3
       DEFANS(MS+6) = CON2
       DEFANS(MS+11) = 1.0
C...   DEFANS(3,4,7,8,9,10,12) = 0
       GO TO 380
C
C...  CALCULATE ZXROT
C
  370  DEFANS(MS+1) = CON2
       DEFANS(MS+3) = -CON3
       DEFANS(MS+6) = 1.0
       DEFANS(MS+9) = CON3
       DEFANS(MS+11) = CON2
C...   DEFANS(MS+2,4,5,7,8,10,12) = 0
C...     TEST FLAG TO SEE WHETHER TO GO TO TAKE MATRIX PRODUCT,
C...     OR TO EXIT
  380       IF(NOMAT)  50, 1000, 510
C
  393  JSUBER = 1003
       GO TO 1000
C
C...     MATRIX/SCALE,A
C
  400  DEFANS(MS+1) = DEFTAB(LM+2)
       DEFANS(MS+6) = DEFTAB(LM+2)
       DEFANS(MS+11) = DEFTAB(LM+2)
C...     TEST FLAG TO SEE WHETHER TO GO TO TAKE MATRIX PRODUCT,
C...     OR TO EXIT
            IF(NOMAT)  50, 1000, 510
C
C...     MATRIX/MAT1,MAT2
C
  500  DO 505  J = 1,12
       XMAT1(J) = DEFTAB(J+2)
  505  XMAT2(J) = DEFTAB(J+15)
       GO TO 518
C
C...     SET UP RESULTS OF VARIED DEFINITION FORMATS FOR PRODUCT
  510       IF(INTRA*INROT.NE.0) GO TO 514
       DO 512  J = 1,12
       XMAT1(J) = DEFANS(J)
  512  XMAT2(J) = DEFANS(J+12)
       GO TO 518
C
  514  DO 516  J = 1,12
       XMAT1(J) = DEFANS(J+12)
  516  XMAT2(J) = DEFANS(J)
C
  518  DO 520  J = 1,3
       XMAT1(J+12) = 0.0
      XMAT2(J+12)=0.0
  520 CONTINUE
      XMAT1(16)=1.0
      XMAT2(16)=1.0
       CALL AMATM(4)
      DO 530 J=1,12
      DEFANS(J)=XMAT3(J)
  530 CONTINUE
       GO TO 1000
C
C...     MATRIX/INVERS,MAT1
C
  600 DO 610 J=1,12
      TMATX(J)=DEFTAB(J+3)
  610 CONTINUE
  615  CALL AINVRT
      DO 620 J=1,12
      DEFANS(J)=XMAT2(J)
  620 CONTINUE
       GO TO 1000
C
C...     MATRIX/PLN1,PLN2,PLN3
C
 730   DO 740J=1,4
       TMATX(J) = DEFTAB(J+2)
       TMATX(J+4) = DEFTAB(J+7)
  740  TMATX(J+8) = DEFTAB(J+12)
       TMATX(4) = -TMATX(4)
       TMATX(8) = -TMATX(8)
       TMATX(12) = -TMATX(12)
       GO TO 615
C
C...     PRODUCT OF TWO MATRICES DEFINED BY VARIOUS FORMATS
C
  800  NN = (N-1)/5
C...     SET FLAG TO GET SECOND MATRIX FOR PRODUCT
       NOMAT = -1
       GO TO (810,820,830,840,850),NN
C
C...     COMPUTE BRANCHING INDEX FOR MATRIX FOLLOWING SYMBOLIC ONE
  810  NEXT = N - 5
C...     PREPARE PICKUP INDEX FOR SECOND MATRIX
       NXLOC = 14
C...     GO TO MOVE SYMBOLIC MATRIX INTO DEFANS
       GO TO 870
C
C...     COMPUTE BRANCHING INDEX FOR MATRIX FOLLOWING
C...     XY-, YZ-, OR ZXROT,A
  820  NEXT = N - 10
C...     PREPARE PICKUP INDEX FOR SECOND MATRIX
       NXLOC = 3
       INROT = 1
C...     GO TO COMPUTE MATRIX FROM GIVEN ROTATION
       GO TO 300
C
C
C...     COMPUTE BRANCHING INDEX FOR MATRIX FOLLOWING TRANSL,A,B,C
  830  NEXT = N - 15
C...     PREPARE PICKUP INDEX FOR SECOND MATRIX
       NXLOC = 5
       INTRA = 1
C...     GO TO COMPUTE MATRIX FROM GIVEN ORIGIN
       GO TO 200
C
C...     COMPUTE BRANCHING INDEX FOR MATRIX FOLLOWING TRANSL,A,B
  840  NEXT = N - 20
C...     PREPARE PICKUP INDEX FOR SECOND MATRIX
       NXLOC = 4
       INTRA = 1
C...     GO TO COMPUTE MATRIX FROM GIVEN ORIGIN
       GO TO 205
C
C...     COMPUTE BRANCHING INDEX FOR MATRIX FOLLOWING SCALE,A
  850  NEXT = N - 25
C...     PREPARE PICKUP INDEX FOR SECOND MATRIX
       NXLOC = 3
C...     GO TO COMPUTE MATRIX FOR GIVEN SCALE
       GO TO 400
C
C...     MOVE SYMBOLIC MATRIX FROM DEFTAB TO DEFANS
  870  DO 875  J = 1,12
       DEFANS(MS+1) = DEFTAB(LM+2)
       MS = MS + 1
  875  LM = LM + 1
C...     TEST FLAG TO SEE WHETHER THIS IS 1ST OR 2ND MATRIX FOR PRODUCT
            IF(NOMAT)  50, 1000, 510
C
C...     MATRIX/PT1,VEC1,VEC2
C
  900  CALL ACROSS(DEFTAB(7),DEFTAB(11),DEFANS(9))
       CALL AZVECT(DEFANS(9))
            IF(JSUBER.NE.0) GO TO 1000
       CALL ACROSS(DEFANS(9),DEFTAB(7),DEFANS(5))
       DO 905  J = 1,3
  905  DEFANS(J) = DEFTAB(J+6)
       DO 910  J = 1,9,4
       CALL ANORM(DEFANS(J),DEFANS(J))
       CALL ADOT(DEFANS(J),DEFTAB(3),DEFANS(J+3))
  910  DEFANS(J+3) = -DEFANS(J+3)
       DO 915  J = 1,12
  915  TMATX(J) = DEFANS(J)
       GO TO 615
C
C...     MATRIX/MIRROR,XY-, YZ-, OR ZXPLAN,OPTIONAL ONE OR TWO
C...            MORE MODIFIERS OF SAME TYPE AS FIRST ONE
C
C...     INITIALIZE VALUES FOR THE MATRIX
  925  DEFANS(1) = 1.0
       DEFANS(6) = 1.0
       DEFANS(11) = 1.0
C...     ADJUST MATRIX ACCORDING TO INPUT MODIFIERS
       KK = IDFTAB(2)
       DO 940  J = 3,KK
       LDF4 = 2*J - 1
            IF(IDFTAB(LDF4+1).EQ.33) GO TO 930
            IF(IDFTAB(LDF4+1).EQ.37) GO TO 935
            IF(IDFTAB(LDF4+1).NE.41) GO TO 393
C...     SET MATRIX VALUE FOR ZXPLAN
       DEFANS(6) = -1.0
       GO TO 940
C...     SET MATRIX VALUE FOR XYPLAN
  930  DEFANS(11) = -1.0
       GO TO 940
C...     SET MATRIX VALUE FOR YZPLAN
  935  DEFANS(1) = -1.0
  940  CONTINUE
       GO TO 1000
C
C...     MATRIX/MIRROR,PLANE OR LINE
C
  950  DEFTAB(7) = -DEFTAB(7)
       SQ = DEFTAB(4)**2 + DEFTAB(5)**2 + DEFTAB(6)**2
       K = 0
C
       DO 965  I = 1,3
       DO 965  J = 1,4
       K = K + 1
       DEFANS(K) = -2.0*DEFTAB(I+3)*DEFTAB(J+3)
            IF(I.NE.J) GO TO 965
       DEFANS(K) = DEFANS(K) + SQ
  965  CONTINUE
C
 1000  RETURN
       END

