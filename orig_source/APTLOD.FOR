       SUBROUTINE APTLOD
C     *** THIS PROGRAM LAST MODIFIED FOR VERSION 4, MODIFICATION 3 ***
C....
C...   THIS SUBROUTINE TAKES AN ICLASS/ELMENT ENTRY (THAT IS THE PARMS
C.. FOUND TO THE RIGHT OF THE MAJOR WORD--OR SLASH) AND CREATES THE
C..  PTPP CODED ENTRIES (PICKUP INDEX IS ISTARP,OUTPUT INPTP)
C***      ****       ****       ****MB****       ******      ***SJD***
C
       IMPLICIT REAL*8(A-H,O-Z)
       REAL*8 NAME,KRFSYS
       INTEGER DEBUG,SUM
       INTEGER*2 ITYPE/0/,IPUNCT/0/
       LOGICAL*1 LCLAS1(2400), ITYPE1(4)
       COMMON/A1COM/NAME,KRFSYS,SUM,JSUBER,NAMSUB,DEBUG,INDXPT,
     1 LOOP,IFINI
       COMMON/AILMTB/ELMENT(600),ICLASS(600),JLMENT
       COMMON/APTPP/PT(300),INPTP,NW,JMODE,KLASS,KLASTP,IRECN,NPT,NPR
       COMMON/APRTAB/ISTARP,IENDP,NMOVE,NL,ITSQ,LINDX
       COMMON/AVST/VST(2750),PTPP(2225),CANON(2225)
       COMMON/AMOTCM/PINT(30),IFURST,JPTIND,NOW,JSUB,KRESLT,KRSLT2
     1,NWDS,ITS,NEXT,MULTAX
       DIMENSION LMENT4(1200)
       DIMENSION IVST(5500)
       DIMENSION IPT(600)
       EQUIVALENCE (ICLASS(1),LCLAS1(1)),(ELMENT(1),LMENT4(1))
       EQUIVALENCE (VST(1),IVST(1))
       EQUIVALENCE (PT(1),IPT(1))
       EQUIVALENCE (ITYPE,ITYPE1(3)),(IPUNCT,ITYPE1(1))
C
C...     SAVE INDEX TO START OF PTPP ENTRY TO BE CONSTRUCTED
       NOW = INPTP
C...     CLEAR FLAG USED TO DESCRIBE PUNCTUATION FOUND WITH A PARAMETER
       IPNTYP = 0
C
C...     INITIALIZE FIRST 2 WORDS OF PTPP ENTRY TO ZERO
   50  PT(INPTP) = 0.0
       PT(INPTP+1) = 0.0
C...     COMPUTE PTPP INTEGER STORING INDEX
   60  INPT4 = 2*INPTP - 1
C
C...     COMPUTE 1/2- AND 1/4-ENTRY PICK-UP INDEXES FOR ICLASS/ELMENT
       ICL2 = 2*ISTARP - 1
       ICL1 = 4*ISTARP - 3
C
C...     GET TYPE OF ICLASS/ELMENT ENTRY
       ITYPE1(4) = LCLAS1(ICL1+3)
C...     IF NOT PUNCTUATION, CLEAR COMMA COUNTER
            IF(ITYPE.EQ.4) GO TO 80
       IFURST = 0
C...     BRANCH ON ENTRY TYPE
   80       IF(ITYPE.EQ.11) GO TO 550
            IF(ITYPE.GT.0) GO TO 90
       JSUBER = 6133
       GO TO 730
   90  GO TO (100,200,300,400,500,600),ITYPE
C
C...     VARIABLE SYMBOL - PICK UP NAME
  100  NAME = ELMENT(ISTARP)
C...     DETERMINE CLASSIFICATION OF SYMBOL
       CALL AVS1CK(JR)
C....
      GOTO (106,104,110,102,110,110,132,110,104),JR
C...
C...   SET UP THE PTPP ENTRY CODE FOR THIS STATEMENT I.D.
  102  IPT(INPT4) = 7
C...     ALSO SET INDICATOR CODE
       JPTIND = 5
       GO TO 120
C
C...     SET PTPP ENTRY TYPE CODE FOR SURFACE
  104  IPT(INPT4) = 4
C...     ALSO SET INDICATOR CODE
       JPTIND = 4
       GO TO 120
C
C...     UNDEFINED - PUT IT IN VST IF THERE IS ROOM
  106  CALL AVS1PT(JS)
            IF(JS.NE.2) GO TO 108
C...  VST OVERFLOW NO ROOM TO ENTER THIS VARIABLE SYMBOL
       JSUBER = 15
       SUM = 1
       GO TO 700
C
C...     THERE WAS ROOM - SET VST ENTRY CODE TO REFERENCED
  108  NAMS4 = 2*NAMSUB - 1
       IVST(NAMS4+2) = 8
       IVST(NAMS4+3) = 0
C
C...     SET PTPP ENTRY TYPE CODE FOR SCALAR
  110  IPT(INPT4) = 2
C...     ALSO SET INDICATOR CODE
       JPTIND = 3
C
C...     PUT VST INDEX TO SYMBOL INTO PTPP ENTRY
  120  IPT(INPT4+3) = NAMSUB
C...     TEST FOR FOLLOWING SUBSCRIPT
       ITYPE1(4) = LCLAS1(ICL1+7)
            IF(ITYPE.LT.7) GO TO 122
            IF(ITYPE.NE.11) GO TO 125
C
C...     NO SUBSCRIPT - SEE WHETHER SYMBOL REQUIRES ONE
  122       IF(JR.LT.6) GO TO 700
C...     YES - ERROR
C.. NO SUBSCRIPT GIVEN FOR A VARIABLE THAT APPEARED IN A RESERV STMT.
       JSUBER = 105
       SUM = 1
       GO TO 700
C
C...     SUBSCRIPT FOUND - SEE WHETHER SYMBOL SHOULD HAVE ONE
  125       IF(JR.GE.6) GO TO 150
C...  SUBSCRIPT GIVEN FOR A VAR. SYMB. NOT GIVEN IN A RESERV STMT
  130  JSUBER = 108
      IF(ITYPE.NE.8) GOTO 135
C... OUTPUT THE NUMERIC SUBSCRIPT FOR THIS VAR. IN ERROR
      PT(7) = ELMENT(ISTARP+1)
      SUM = 9
      GO TO 170
C.......
C... VAR. IS A PREVIOUSLY DEFINED MACRO NAME      -- ERROR
  132 JSUBER = 211
C...     SET INDICATOR CODE FOR ERROR ALSO
       JPTIND = 8
C
C...     SET FLAG TO INCLUDE SYMBOL IN DIAGNOSTIC, AND OUTPUT IT
  135  SUM = 1
       GO TO 170
C
C...     SUBSCRIPT REQUIRED AND FOUND - BRANCH ON ITS TYPE
  150       IF(ITYPE-8)  155, 160, 165
C
C...     SUBSCRIPT IS VARIABLE SYMBOL - PUT CODE INTO PTPP
  155  IPT(INPT4+1) = 2
C...     PUT VST INDEX TO SUBSCRIPT SYMBOL INTO PTPP
       IPT(INPT4+2) = LMENT4(ICL2+3)
       GO TO 170
C
C...     SUBSCRIPT IS NUMBER - PUT CODE INTO PTPP
  160  IPT(INPT4+1) = 1
C...     PUT VALUE OF SUBSCRIPT INTO THIRD WORD OF PTPP ENTRY
       PT (INPTP+2) = ELMENT(ISTARP+1)
C...     INCREMENT PTPP STORING INDEX BY ONE TO ACCOUNT FOR
C...     THE EXTRA WORD FOR THIS SUBSCRIPT IN THE PTPP ENTRY
       INPTP = INPTP + 1
       GO TO 170
C
C...     SUBSCRIPT IS A COMPUTED EXPRESSION OR AN INCLUSIVE FORMAT -
C...     TEST FOR EXPRESSION
  165       IF(ITYPE.NE.9) GO TO 167
C...     YES - PUT CODE INTO PTPP
       IPT(INPT4+1) = 3
C...     GO TO PUT SCALAR TABLE INDEX TO SUBSCRIPT VALUE INTO PTPP
       GO TO 168
C
C...     INCLUSIVE SUBSCRIPT FORMAT - PUT CODE INTO PTPP
  167  IPT(INPT4+1) = 4
C...     PUT SCALAR TABLE INDEX TO SUBSCRIPT VALUE, OR INDEX TO
C...     STORAGE FOR DATA BLOCK POINTER, INTO PTPP
  168  IPT(INPT4+2) = LMENT4(ICL2+3)
C
C...     INCREMENT ICLASS/ELMENT PICK-UP INDEX ONE TO ACCOUNT
C...     FOR THE SUBSCRIPT ENTRY FOR THIS SYMBOL
  170  ISTARP = ISTARP + 1
       GO TO 700
C
C...     VOCABULARY WORD - SET PTPP ENTRY TYPE CODE
  200  IPT(INPT4) = 6
       KRESLT = LMENT4(ICL2+1)
C...     PICK UP VOCABULARY SUBTYPE
  210  KRSLT2 = LMENT4(ICL2)
C...     SEE IF WORD IS MAJOR OR MINOR - INITIALIZE INDICATOR FOR MINOR
       JPTIND = 0
       ITYPE1(4) = LCLAS1(ICL1+2)
            IF(ITYPE.EQ.29) GO TO 310
C...     MAJOR - SET INDICATOR
       JPTIND = 1
C...     GO TO PUT PROTAP CLASS AND SUBCLASS CODES (IN ELMENT)
C...     INTO THE 2ND WORD OF THIS PTPP ENTRY
       GO TO 310
C
C...     NUMBER - SET PTPP ENTRY TYPE CODE
  300  IPT(INPT4) = 1
C...     ALSO SET INDICATOR CODE
       JPTIND = 2
C
C...     PUT VALUE OF NUMBER, OR PROTAP CODES FOR A VOCABULARY
C...     WORD, INTO 2ND WORD OF PTPP ENTRY
  310  PT(INPTP+1) = ELMENT(ISTARP)
       GO TO 700
C
C...     PUNCTUATION - SET INDICATOR FOR IT, AND PICK UP TYPE
  400  JPTIND = 9
       ITYPE1(4) = LCLAS1(ICL1+2)
       KRSLT2 = ITYPE
C...     TEST FOR A COMMA
            IF(ITYPE.NE.9) GO TO 430
C
C...     YES - IF THIS IS A COMPUTING EXPRESSION, ERROR
            IF(ITSQ.NE.1) GO TO 405
       JSUBER = 7
       GO TO 730
C
C...     NO - IF COMMA IMMEDIATELY FOLLOWS A SIGN, ERROR
  405       IF(IPNTYP.LE.0) GO TO 410
       ISTARP = ISTARP + 1
       GO TO 735
C
C...     IF SCANNING GEOMETRIC FORMAT, TEST FOR 2ND SUCCESSIVE COMMA
  410       IF(ITSQ.NE.3) GO TO 420
            IF(IFURST.EQ.1) GO TO 415
C...     NO - COUNT THIS COMMA, AND EXIT
       IFURST = 1
       GO TO 730
C
C...     SECOND CONSECUTIVE COMMA - SET TYPE CODE TO OUTPUT SPECIAL
C...     PUNCTUATION ENTRY FOR IT
  415  ITYPE = 10
       GO TO 435
C
C...     NO - TEST FOR AUTOMATIC PICK-UP OF ENTRY FOLLOWING COMMA - IF
C...     NOT, JUST DELETE COMMA AND EXIT
  420       IF(ITSQ-4)  730, 425, 730
C...     PICKING UP NEXT ENTRY - SET FLAG FOR COMMA FOUND, AND GO TO
C...     SPACE PAST IT
  425  IPNTYP = -1
       GO TO 440
C
C...     NOT A COMMA - CLEAR COMMA COUNTER
  430  IFURST = 0
C...     SET PTPP ENTRY TYPE CODE FOR PUNCTUATION
  435  IPT(INPT4) = 8
C...     PUT PUNCTUATION CODE INTO PTPP ENTRY
       IPT(INPT4+1) = ITYPE
C...     INCREMENT PTPP STORING INDEX BY ONE HERE FOR THIS PUNCTUATION
C...     ENTRY, AND SKIP INCREMENTING OF THIS INDEX LATER
       INPTP = INPTP + 1
C...     IF NOT SCANNING A COMPUTING EXPRESSION, SEE IF THIS IS A SIGN
            IF(ITSQ.EQ.1) GO TO 730
            IF(ITYPE.GT.2) GO TO 730
C...     SIGN FOUND - MAKE SURE IT IS NOT SECOND CONSECUTIVE ONE
            IF(IPNTYP.GT.0) GO TO 735
C...     SET FLAG FOR SIGN, SKIP IT, AND GO TO GET NEXT ENTRY, IF ANY
       IPNTYP = 1
C
  440  ISTARP = ISTARP + 1
C...     MAKE SURE THERE IS ANOTHER ENTRY - IF NOT, ERROR
            IF(ISTARP.LE.JLMENT) GO TO 50
       JSUBER = 129
       GO TO 745
C
C...     NESTED COMPUTING EXPRESSION - SET PTPP ENTRY TYPE CODE
  500  IPT(INPT4) = 3
C...     SET INDICATOR CODE FOR NUMBER
       JPTIND = 2
C...     GO TO PUT INDEX TO STORAGE FOR VALUE OF EXPRESSION
C...     INTO PTPP ENTRY
       GO TO 610
C
C...     NESTED ARGUMENT LIST - SET PTPP ENTRY TYPE CODE
  550  IPT(INPT4) = 9
C...     GO TO PUT INDEX TO STORAGE FOR POINTER TO DATA BLOCK
C...     INTO PTPP ENTRY
       GO TO 610
C
C...     NESTED SURFACE DEFINITION - SET PTPP ENTRY TYPE CODE
  600  IPT(INPT4) = 5
C...     ALSO SET INDICATOR CODE
       JPTIND = 4
C...     PUT INDEX TO LOCATION FOR STORAGE OF SURFACE, OR INDEX
C...     TO STORAGE FOR VALUE OF EXPRESSION, INTO PTPP ENTRY
  610  IPT(INPT4+3) = LMENT4(ICL2+1)
C
C...     INCREMENT PTPP STORING INDEX FOR NORMAL 2-WORD ENTRY - IF 3RD
C...     WORD WAS REQUIRED, STORING INDEX ALREADY INCREMENTED FOR IT
  700  INPTP = INPTP + 2
C...     INCREMENT PICK-UP INDEX PAST ICLASS/ELMENT ENTRY JUST READ
  730  ISTARP = ISTARP + 1
C
C...     TEST FOR SIGN FOUND PRECEDING PARAMETER
            IF(IPNTYP.LE.0) GO TO 740
C...     YES - PARAMETER MUST BE A SCALAR, OR ERROR
            IF(JPTIND.EQ.2) GO TO 740
            IF(JPTIND.EQ.3) GO TO 740
  735  JSUBER = 78
C
C...     IF ERROR FLAG WAS SET IN THIS ROUTINE, ADJUST RETURN INDICATOR
  740       IF(JSUBER.EQ.0) GO TO 750
  745  JPTIND = 8
C
C...     RETURN TO CALLING ROUTINE
  750       IF(DEBUG.EQ.0) GO TO 900
       WRITE (6,800) ICLASS(ISTARP-1),ELMENT(ISTARP-1),JPTIND,KRESLT
     1 ,KRSLT2,IPNTYP
  800  FORMAT (10H0   APTLOD,Z12,Z20,I3,3Z12)
  900  RETURN
       END

